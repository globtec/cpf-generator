' Gambas class file

Public cpf[11] As String

Public Sub _new()

End

Public Sub Form_Open()
  
  Me.Center
  
End

Public Sub txtCpf_KeyRelease()

  Dim i As Integer
  
  txtCpf.Foreground = &000000&
  
  btnCopy.Enabled = False
  btnValidate.Enabled = False
  btnClean.Enabled = False

  If Not IsNumber(txtCpf.Text) Then
    txtCpf.Clear
  Endif
  
  If Len(txtCpf.Text) > 0 Then
    btnClean.Enabled = True
  Endif
  
  If 11 = Len(txtCpf.Text) Then
    
    For i = 1 To 11
      cpf[i - 1] = Mid(txtCpf.Text, i, 1)
    Next
    
    btnValidate.Enabled = True
    
  Endif

End

' Shows the generated cpf
Public Sub btnGenerate_Click()
  
  txtCpf.Foreground = &000000&
  txtCpf.Text = generate()
  
  btnValidate.Enabled = False
  btnClean.Enabled = True
  
  If Not btnCopy.Enabled Then
    btnCopy.Enabled = True
  Endif
  
End

' Copy Text into the Clipboard 
Public Sub btnCopy_Click()
  
  Clipboard.Copy(txtCpf.Text)
  
End

' Check cpf
Public Sub btnValidate_Click()

  If cpf[9] = firstCheckDigit() And cpf[10] = secondCheckDigit() Then
    txtCpf.Foreground = &H3F7F00&
    btnCopy.Enabled = True
  Else
    txtCpf.Foreground = &HFF0000&
    btnCopy.Enabled = False
  Endif
  
  btnValidate.Enabled = False

End

Public Sub btnClean_Click()

  txtCpf.Clear
  btnClean.Enabled = False

End

Public Function validate() As Boolean
  
  Return cpf[9] = firstCheckDigit() And cpf[10] = secondCheckDigit()
  
End

' Generating a CPF 
Public Function generate() As String
  
  Dim i As Integer
  
  For i = 0 To 8
    cpf[i] = Int(Rnd(0, 9))
  Next
  
  cpf[9] = firstCheckDigit()
  cpf[10] = secondCheckDigit()
  
  Return cpf.Join("")
  
End

' Calculating the first check digit
Public Function firstCheckDigit() As Integer
  
  Dim i, value As Integer
  
  value = 0
  
  For i = 0 To 8
    value += cpf[i] * (10 - i)
  Next
  
  If (value Mod 11) < 2 Then
    Return 0
  Endif
  
  Return 11 - (value Mod 11)
  
End

' Calculating the second check digit
Public Function secondCheckDigit() As Integer
  
  Dim i, value As Integer
  
  value = 0
  
  For i = 1 To 8
    value += cpf[i] * (11 - i)
  Next
  
  value += firstCheckDigit() * 2
  
  If (value Mod 11) < 2 Then
    Return 0
  Endif
  
  Return 11 - (value Mod 11)
  
End
